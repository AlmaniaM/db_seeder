plugins {
    id 'java'
}

group 'ch.konnexions'
version '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.microsoft.sqlserver:mssql-jdbc:8.3.1.jre14-preview'
    implementation 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'mysql:mysql-connector-java:8.0.20'
    implementation 'org.apache.commons:commons-configuration2:2.7'
    implementation 'org.slf4j:slf4j-log4j12:1.7.25'
}

jar {
    archivesBaseName = project.name

    manifest {
        attributes 'Main-Class' : 'ch.konnexions.db_seeder.DatabaseSeeder'
    }
}

task fatJar(type: Jar) {
    group = "Konnexions"
    description = 'Create a jar file including all dependencies'

    manifest.from jar.manifest
    archiveClassifier  = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task copyJarToLib(type: Copy, dependsOn: 'fatJar') {
    group = "Konnexions"
    description = 'Create the jar file and copy it to the lib directory'

    from "$buildDir/libs"
    rename project.name + '-' + version + '-all.jar', project.name + '.jar'
    into "lib"
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}
